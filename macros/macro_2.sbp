'!FABMO!name: Z-Zero
'!FABMO!description: Set Z-Zero with Zeroing Plate; after changing cutter
' Name: Z-Zero
' Description: 
'   Set Z-Zero with Zeroing Plate; Use after installing new cutter [C2]
' Platform: ShopBot DT Tools
' Author: Ted Hall, Ryan Sturmer
' History:
'   2016/03/01 - Initial version
'   2017/09/25 - Refactored (th)
'   2019/10/25 - Refactored (th)

SU, 0                         ' Set to Inches; Units will auto-revert to current starting units at end of file

'--- Settings ---             ' Initializing permanent variables; ##:=## if not already set
$current_cutter_Zoffset := .25' This variable tracks CUTTER LENGTH as will be set here with Macro#2 (C2); it is used in C3
$z_backoff := .05
$ZZeroInput := 1 
$ZZeroPlateThickness := 0.118 ' 0.118" for std plate, your plate may be slightly different
  ' To instead use ShopBot/Handibot pressure plate, change &ZZeroInput to 5, and
  ' ... adjust plate thickness to 0.57, and
  ' ... and change %(51) to %(55) in check_input subroutine

'--- Locals Variables ---
&homing_input = 3             ' Input# of prox switch; all DT switches are in series on inp3
&z_search_dist = 8.0
&z_search_speed = .75
&z_slow_search_speed = .15
&ZZero_search_dist = -6.0 
&ZZero_slow_search_speed = 0.15
&safeZ_pullup = .25           ' ##Just hardcoded at moment ... will eventually be read from config value, or system_var 28
&temp_offset = 0

MS, 4, 4					  ' Speeds used in Macro, will auto-revert back to current values at end of file
JS, 5, 5

' --- Main Program Section ---
ZZ
GOSUB check_input
GOSUB initial_home_z
GOSUB find_z_zero
GOSUB finish
END

' --- Subroutines ---
initial_home_z:
	PZ, &z_search_dist, &z_search_speed, &homing_input    ' PZ "probes" for a switch or contact in the Z axis
    ZZ
    JZ, (-1 * $z_backoff)
    &target_ck = $z_backoff + 0.05                        ' Target just past prox
    PZ, &target_ck, &z_slow_search_speed, &homing_input   ' Slowly search for prox in accurate check
    ZZ
    JZ, (-1 * $z_backoff)
    VA,,,0,,,,,,0,,,                                      ' Set 0 location to prox
	RETURN

find_z_zero:                                              ' Getting Offset for this cutter
   PZ, &ZZero_search_dist, &z_search_speed, $ZZeroInput   
   Pause .5
   &temp_offset = %(3)
   JZ, (&temp_offset + $z_backoff) 
   PZ, (&temp_offset - (2 * $z_backoff)), &ZZero_slow_search_speed, $ZZeroInput
   Pause .5
   &temp_off = %(3)                                       ' Should be accurate distance from Z zero 
   &temp_off = &temp_off * -1
   $current_cutter_Zoffset = &temp_off  + $ZZeroPlateThickness   
   JZ, ((-1 * $current_cutter_Zoffset) + $z_backoff + &safeZ_pullup) 
   VA,,, ($z_backoff + &safeZ_pullup)                     ' Only setting local Z, not TBC-Z
   RETURN

finish:
    Pause "Remember to Remove Clip! The following value will be used for Cutter Offset in Homing:  " + $current_cutter_Zoffset
    RETURN

missed_target:
    FAIL "Target Not Triggered!"                           

check_input:
    IF %(51) = 1 THEN GOTO check_input_fail_plate
    IF %(53) = 1 THEN GOTO check_input_fail_prox
	RETURN

check_input_fail_plate:
    FAIL "Z-zero is already triggered. Cannot zero the tool. Move Off and Retry."
check_input_fail_prox:
    FAIL "A prox switch is already triggered. Cannot zero the tool. Move Off and Retry."
