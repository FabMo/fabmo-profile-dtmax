'!FABMO!name: Home Tool
'!FABMO!description: Automatically Set Axis Zero for XYZ
' Name: Home Tool
' Description: Automatically Set Axis Zero for XYZ [C3]
' Platform: ShopBot DT Tools
' Author: Ryan Sturmer, Ted Hall
' History:
'   2017/08/01 - Initial version (rs)
'   2017/09/15 - Refactored (th); use persistent offsets, manage z
'   2019/10/25 - Refactored (th); small updates for DT FabMo release

SU,0                   ' Set to Inches; Units will auto-revert to current starting units at end of file

'--- Settings ---      ' Initializing permanent variables; ##:=## if not already set
$x_backoff := .25
$y_backoff := .25 
$z_backoff := .25
$home_position_x := 0
$home_position_y := 0
$current_cutter_Zoffset := .25 ' ...just in case tool has never Z-Zeroed
$dt_type := 3          ' 2=DTmax; 3=DT3

'--- Locals Variables ---
&homing_input = 3      ' Input # for prox switch; all DT switches are in series on inp3
&xy_search_speed = 2
&z_search_speed = 1.5
&xy_slow_search_speed = .15
&z_slow_search_speed = .15
&z_search = 8.0
IF $dt_type = 3 then GOTO dt3
    &x_search = -38.0
    &y_search = -26.0
    GOTO speeds_next
dt3:
    &x_search = -26.0
    &y_search = -20.0

speeds_next:
    MS, 4, 4           ' Speeds used in this Macro, will auto-revert back to current values at end of file
    JS, 5, 5

' --- Main Program Section ---
Z3
GOSUB backoff_if_needed
GOSUB home_z
GOSUB home_x
GOSUB home_y
GOSUB go_home
GOSUB offset_z_cutter  ' Adds the offset for Z cutter; SEE COMMENTS IN SUBROUTINE
END

' --- Subroutines ---
home_z:
	PZ, &z_search, &z_search_speed, &homing_input         ' PZ "probes" for a switch or contact in the Z axis
    ZZ
    JZ, (-1 * $z_backoff)
    &target_ck = $z_backoff + 0.05                        ' Target just past prox
    PZ, &target_ck, &z_slow_search_speed, &homing_input   ' Slowly search for prox in accurate check
    Pause .5
    IF %(3) = &target_ck THEN GOSUB missed_target
    ZZ
    JZ, (-1 * $z_backoff)
    VA,,,0,,,,,,0,,,                                      ' Set 0 location
	RETURN

offset_z_cutter:
    Pause "Click RESUME to continue to set Z offset to last cutter measurement with ZZero"
    VA,,,($current_cutter_Zoffset),,,,,,0,,
    RETURN

home_x:
    PX, &x_search, &xy_search_speed, &homing_input        
    ZX
    JX, $x_backoff
    &target_ck = (-1 * (0.05 + $x_backoff))               
    PX, &target_ck, &xy_slow_search_speed, &homing_input 
    Pause .5
    IF %(1) = &target_ck THEN GOSUB missed_target
    ZX
    JX, $x_backoff
	VA,0,,,,,,0,,,,,                                     
	RETURN

home_y:
	PY, &y_search, &xy_search_speed, &homing_input  
    ZY
    JY, $y_backoff
    &target_ck = (-1 * (0.05 + $y_backoff))     
    PY, &target_ck, &xy_slow_search_speed, &homing_input  
    Pause .5
    IF %(2) = &target_ck THEN GOSUB missed_target
    ZY
    JY, $y_backoff
	VA,,0,,,,,,0,,,,                                     
	RETURN

go_home:
	J2, $home_position_x, $home_position_y
	Z2                                                    ' Zero alternate home location if one defined (local zero)
    RETURN

backoff_if_needed:
    IF %(53) = 1 THEN GOSUB backoff                       ' Some proximity switch was on
    IF %(53) = 1 THEN GOSUB no_clear
    RETURN
    
backoff:                                                  ' Make a 3-axis move to back off any of  switches
    M3, $x_backoff, $y_backoff, (-1 * $z_backoff)
    RETURN

missed_target:
    FAIL "Target Not Triggered!"                          ' Did not hit target.

no_clear:
    FAIL "Proximity Switch Not Clear!"                    ' Could not clear.
